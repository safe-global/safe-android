apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'bivrost'
apply plugin: 'io.fabric'
apply from: '../buildsystem/coverageReport.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    defaultConfig {
        applicationId "pm.gnosis.heimdall"
        versionCode getInt("APP_VERSION_CODE", 702)
        versionName getKey("APP_VERSION_NAME", "0.4.1-alpha")
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField javaTypes.STRING, "PROXY_FACTORY_ADDRESS", asString(getKey("PROXY_FACTORY_ADDRESS", "0x5a27c4325b2f35163fa28d9195e7038fb53e67ac"))
        buildConfigField javaTypes.STRING, "SAFE_MASTER_COPY_ADDRESS", asString(getKey("SAFE_MASTER_COPY_ADDRESS", "0x057ad4d88c1aabecf899f33386c8abebabe00648"))
        buildConfigField javaTypes.STRING, "BLOCKCHAIN_NET_URL", asString(getKey("BLOCKCHAIN_NET_URL", "https://rinkeby.infura.io"))
        buildConfigField javaTypes.STRING, "INFURA_API_KEY", asString(getKey("INFURA_API_KEY", ""))

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        project.ext.set("archivesBaseName", String.format("gnosis-safe-%s", defaultConfig.versionCode))

        ext.betaDistributionGroupAliases = System.getenv("FABRIC_GROUP_INTERNAL_BETA")
        ext.betaDistributionReleaseNotes = System.getenv("APP_RELEASE_NOTES")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    signingConfigs {
        debug {
            // We add the debug keystore to repo so that the travis build will always have the same signature
            storeFile file("../debug.keystore")
        }
        release {
            storeFile file(getKey("SIGNING_KEYFILE_PATH", "../gnosis-upload.jks"))
            storePassword getKey("SIGNING_STORE_PASSWORD")
            keyAlias "gnosis"
            keyPassword getKey("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
        }

        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }

        release {
            minifyEnabled true
            multiDexEnabled false
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }
}

jacoco {
    toolVersion = versions.jacoco
    version = versions.jacoco
}

dependencies {
    // We specify junit before anything else to make sure that this version is prioritized over the
    // one bundled in the android.jar
    testImplementation "junit:junit:$versions.junit"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Gnosis Svalinn
    implementation "com.github.gnosis.svalinn-kotlin:accounts-kethereum:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:android-common:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:blockies:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:crypto:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:ethereum:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:ethereum-rpc:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:ethereum-rpc-retrofit:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:erc67:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:mnemonic-android:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:ticker:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:security:$versions.svalinn"
    implementation "com.github.gnosis.svalinn-kotlin:utils:$versions.svalinn"

    // Gnosis Bivrost
    implementation "com.github.gnosis.bivrost-kotlin:bivrost-solidity-types:$versions.bivrost"

    // Fabric
    implementation("com.crashlytics.sdk.android:crashlytics:$versions.crashlytics@aar") {
        transitive = true
    }

    // Kethereum RLP
    implementation "com.github.walleth.kethereum:rlp:$versions.kethereum"

    // Android
    implementation "com.android.support:appcompat-v7:$versions.android_support"
    implementation "com.android.support:cardview-v7:$versions.android_support"
    implementation "com.android.support:design:$versions.android_support"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraint_layout"
    implementation "com.android.billingclient:billing:$versions.billing"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxkotlin"
    implementation "com.gojuno.koptional:koptional:$versions.koptional"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"

    // Android Architecture Components
    kapt "android.arch.lifecycle:compiler:$versions.architecture_components"
    implementation "android.arch.persistence.room:runtime:$versions.architecture_components"
    kapt "android.arch.persistence.room:compiler:$versions.architecture_components"
    implementation "android.arch.persistence.room:rxjava2:$versions.architecture_components"
    implementation "android.arch.lifecycle:extensions:$versions.android_lifecycle"
    implementation "com.android.support:multidex:$versions.multidex"

    // Firebase
    implementation "com.google.firebase:firebase-core:$versions.firebase"
    implementation "com.google.firebase:firebase-messaging:$versions.firebase"
    implementation "com.google.android.gms:play-services-auth:$versions.firebase"

    // String formatting
    implementation "com.squareup.phrase:phrase:$versions.phrase"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    // Moshi
    implementation "com.squareup.moshi:moshi:$versions.moshi"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"

    // Logging
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // RxBinding
    implementation("com.jakewharton.rxbinding2:rxbinding-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }
    implementation("com.jakewharton.rxbinding2:rxbinding-design-kotlin:$versions.rxbinding") {
        exclude group: "io.reactivex.rxjava2", module: "rxandroid"
    }

    // More
    implementation "com.github.clans:fab:$versions.floating_action_button"
    implementation("me.zhanghai.android.materialprogressbar:library:$versions.material_progressbar") {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    implementation("com.madgag.spongycastle:prov:$versions.spongycastle", {
        exclude group: 'junit', module: 'junit'
    })

    // Testing
    testImplementation "com.github.gnosis.svalinn-kotlin:utils-testing:$versions.svalinn"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "org.mockito:mockito-inline:$versions.mockito"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$versions.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

apply plugin: 'com.google.gms.google-services'