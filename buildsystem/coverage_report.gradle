apply plugin: 'jacoco'

android {
    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

jacoco {
    toolVersion = versions.jacoco
    version = versions.jacoco
}

afterEvaluate { project ->
    def ignoreList = jacocoIgnoreList
    def projectName = project.name
    if (ignoreList.contains(projectName)) {
        println "Jacoco: ignoring project ${projectName}"
        return false
    }
    def threshold = project.hasProperty('jacocoCoverageThreshold')
            ? project.jacocoCoverageThreshold
            : project.jacocoCoverageThresholdDefault
    setupTestExistenceValidationTask()
    if (isAndroidModule(project)) {
        setupAndroidReporting()
        setupAndroidCoverageVerification(threshold)
    } else {
        setupKotlinReporting()
        setupKotlinCoverageVerification(threshold)
    }
}

def setupKotlinReporting() {
    jacocoTestReport {
        dependsOn testDebugUnitTest
        dependsOn createDebugCoverageReport
        reports {
            csv.getRequired().set(false)
            xml.getRequired().set(true)
            html.getRequired().set(true)
            html.getOutputLocation().set(file("${buildDir}/coverage-report"))
        }
    }
}

def setupAndroidReporting() {
    tasks.withType(Test) {
        // Whether or not classes without source location should be instrumented
        jacoco.includeNoLocationClasses true
        jacoco.excludes = ['jdk.internal.*'] // https://github.com/gradle/gradle/issues/5184#issuecomment-457865951
    }

    task jacocoTestReport(
            type: JacocoReport,
            dependsOn: [
                    'testDebugUnitTest',
                    'createDebugCoverageReport'
            ]
    ) {
        reports {
            csv.getRequired().set(false)
            xml.getRequired().set(true)
            html.getRequired().set(true)
            html.getOutputLocation().set(file("${buildDir}/coverage-report"))
        }
        // Change as needed
        def testCoverageFileFilter = [
                '**/*Test*.*',
                '**/AutoValue_*.*',
                '**/*JavascriptBridge.class',
                '**/R.class',
                '**/R$*.class',
                '**/Manifest*.*',
                'android/**/*.*',
                '**/BuildConfig.*',
                '**/*$ViewBinder*.*',
                '**/*$ViewInjector*.*',
                '**/*$InjectAdapter.class',
                '**/*$ModuleAdapter.class',
                '**/*$ViewInjector*.class',
                '**/*_*MembersInjector*.class',
                '**/Dagger*Component*.class', // covers component implementations
                '**/Dagger*Component*$Builder.class', // covers component builders
                '**/*_*Factory*.class',
                '**/*_*Impl*.class'
        ]
        def debugTree = fileTree(
                dir: "$buildDir/tmp/kotlin-classes/debug",
                excludes: testCoverageFileFilter
        )
        def mainSrc = "$projectDir/src/main/java"
        sourceDirectories.setFrom(files([mainSrc]))
        classDirectories.setFrom(files([debugTree]))
        executionData(fileTree(
                dir: project.buildDir,
                includes: [
                        'jacoco/testDebugUnitTest.exec',
                        'outputs/code-coverage/*/connected/*/coverage.ec'
                ]
        ))
    }
}

def setupAndroidCoverageVerification(threshold) {
    task jacocoTestCoverageVerification(
            type: JacocoCoverageVerification,
            dependsOn: [
                    'testDebugUnitTest',
                    'testExistenceValidation'
            ]
    ) {
        violationRules {
            rule {
                limit {
                    minimum = threshold
                }
            }
        }
        final def coverageSourceDirs = [
                "$projectDir/src/main/java",
                "$projectDir/src/main/kotlin"
        ]
        final def debugTree = fileTree(
                dir: "$buildDir/tmp/kotlin-classes/debug",
                excludes: jacocoThresholdFileFilter
        )
        sourceDirectories.setFrom(files(coverageSourceDirs))
        classDirectories.setFrom(files([debugTree]))
        executionData(fileTree(dir: project.buildDir, includes: ['jacoco/${unitTestTask}.exec']))
    }
}

def setupKotlinCoverageVerification(threshold) {
    jacocoTestCoverageVerification {
        dependsOn testDebugUnitTest
        dependsOn testExistenceValidation

        violationRules {
            rule {
                limit {
                    minimum = threshold
                }
            }
        }
    }
}

def setupTestExistenceValidationTask() {
    task testExistenceValidation(type: TestExistenceValidation)
}

project.ext {
    jacocoCoverageThresholdDefault = 0.60

    jacocoIgnoreList = [] // add module names to ignore
    // Exclude file by names, packages or types. Such files will be ignored during test coverage
    // calculation
    jacocoThresholdFileFilter = [
            '**/*App.*',
            '**/*Application.*',
            '**/*Database.*',
            '**/*Activity.*',
            '**/*Fragment.*',
            '**/*Dialog.*',
            '**/*View.*',
            '**/*ViewHolderFactory.*',
            '**/*ViewHolder.*',
            '**/*ViewGroup.*',
            '**/*JsonAdapter.*',
            '**/*PagingSource.*',
            '**/*PagingProvider.*',
            '**/di/**',
            '**/*Dagger.*',
            '**/*Binding*.*',
            'io/gnosis/data/models/**'
    ]
}

private static boolean isAndroidModule(Project project) {
    def isAndroidLibrary = project.plugins.hasPlugin('com.android.library')
    def isAndroidApp = project.plugins.hasPlugin('com.android.application')
    return isAndroidLibrary || isAndroidApp
}

// Modules without any unit tests should also fail validation
class TestExistenceValidation extends DefaultTask {

    static final SRC_DIR = 'src'
    static final JAVA_DIR = 'java'
    static final TEST_DIRS = ['test', 'androidTest']

    static final IGNORED_NAME_PATTERNS = [
            ~/^sample-.++$/
    ]

    @TaskAction
    void execute() {
        if (shouldSkip(project)) return

        File srcDir = new File(project.projectDir, SRC_DIR)
        FileFilter filter = { it.isDirectory() }
        File[] subDirs = srcDir.listFiles(filter) ?: []
        File testsDir = subDirs.find { TEST_DIRS.contains(it.name) }
        if (testsDir) {
            File javaTestsDir = testsDir
                    .listFiles(filter)
                    .find { it.name == JAVA_DIR }
            if (javaTestsDir && javaTestsDir.list().length > 0) {
                return
            }
        }

        throw new GradleException(
                "${project.name} has no unit tests. "
        )
    }

    private static boolean shouldSkip(Project project) {
        def name = project.name
        return IGNORED_NAME_PATTERNS
                .collect { name =~ it } // convert Pattern to Matcher
                .any { it.find() }
    }
}
